# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# set oh-my-zsh home dir and source the initializer
export ZSH=$HOME/.oh-my-zsh
plugins=(brew gitfast rvm vi-mode)
source $ZSH/oh-my-zsh.sh

# your project folder that we can `c [tab]` to
export PROJECTS=~/workspace

# https://github.com/benvan/sandboxd
source $DOTFILES/sandbox/sandboxd

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path, completion files and backup directories
for file in ${${${config_files:#*/path.zsh}:#*/completion.zsh}:##*/backups/*}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Better history
# Credits to https://coderwall.com/p/jpj_6q/zsh-better-history-searching-with-arrow-keys
# and https://github.com/robbyrussell/oh-my-zsh/issues/1720#issuecomment-286366959
if [[ "${terminfo[kcuu1]}" != "" ]]; then
  autoload -U up-line-or-beginning-search
  zle -N up-line-or-beginning-search
  bindkey "${terminfo[kcuu1]}" up-line-or-beginning-search
fi
if [[ "${terminfo[kcud1]}" != "" ]]; then
  autoload -U down-line-or-beginning-search
  zle -N down-line-or-beginning-search
  bindkey "${terminfo[kcud1]}" down-line-or-beginning-search
fi

eval "$(direnv hook zsh)"

export DYLD_LIBRARY_PATH="usrl/local/lib:$DYLD_LIBRARY_PATH"
export HOMEBREW_NO_ANALYTICS=1
